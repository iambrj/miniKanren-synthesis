The grammar of the language we will look at is as follows:

\begin{framed}
\begin{grammar}
<program> ::= "(run 1 ("<id>")" <goal-expr>")"
\end{grammar}

\begin{indentgrammar}{<goal-expr>}
<goal-expr> ::= "(==" <term-expr> <term-expr>")"
         \alt "(fresh" (<id>) <goal-expr>")"
         \alt "(conj" <goal-expr> <goal-expr>")"
         \alt "(disj" <goal-expr> <goal-expr>")"
\end{indentgrammar}

\begin{indentgrammar}{<term-expr>}
<term-expr> ::= "(quote "<value>")"
            \alt <id>
\end{indentgrammar}

\begin{indentgrammar}{<value>}
<value> ::= Any valid Racket symbol

<id> ::= Any valid Racket symbol
\end{indentgrammar}

Term expressions evaluate to terms, whose grammar is:

\begin{indentgrammar}{<term>}
<term> ::= <logic-var> | <symbol>

<logic-var> ::= Interpreting logic variable

<symbol> ::= Any valid Racket symbol
\end{indentgrammar}

The difference between term and values is that terms are unified with logic
variables, but values may not be.
\end{framed}

The shallow interpreter for the above language using the Scheme implementation
of miniKanren is as follows:

\begin{Verbatim}
; Evaluate a program by evaluating the goal
; expression after initializing the
; environment
(define (eval-programo program out)
    (fresh (q ge)
      (== `(run 1 (,q) ,ge) program)
      ; Interpreted logic variables are symbols
      (symbolo q)
      ; The environment (association list) maps
      ; interpreted logic variables to interpreting
      ; logic variables
      (eval-gexpro ge `((,q . ,out)))))

; Evaluate goal expression in an environment
(define (eval-gexpro expr env)
    (conde
      [(fresh (e1 e2 t)
         (== `(== ,e1 ,e2) expr)
         ; Evaluation of both terms should unify to
         ; the same term t
         (eval-texpro e1 env t)
         (eval-texpro e2 env t))]
      [(fresh (x x1 ge)
         (== `(fresh (,x) ,ge) expr)
         (symbolo x)
         ; Translate interpreted fresh logic variable
         ; into interpreting fresh logic variable by
         ; extending the environment
         (eval-gexpro ge `((,x . ,x1) . ,env)))]
      [(fresh (ge1 ge2)
         (== `(conj ,ge1 ,ge2) expr)
         ; Translate interpreted conjunction into
         ; interpreting conjunction
         (eval-gexpro ge1 env)
         (eval-gexpro ge2 env))]
      [(fresh (ge1 ge2)
         (== `(disj ,ge1 ,ge2) expr)
         ; Translate interpreted disjunction into
         ; interpreting disjunction
         (conde
           [(eval-gexpro ge1 env)]
           [(eval-gexpro ge2 env)]))]))

; Evaluate a term expression in an environment
(define (eval-texpro expr env val)
    (conde
      ; Quoted values are self-evaluating
      [(== `(quote ,val) expr)]
      ; Lookup interpreted logic variables in the
      ; environment
      [(symbolo expr) (lookupo expr env val)]))

; Search for a variable in an environment
(define (lookupo x env val)
    (fresh (y v d)
      (== `((,y . ,v) . ,d) env)
      (conde
        [(== x y) (== v val)]
        [(=/= x y)
         (lookupo x d val)])))
\end{Verbatim}

To get an intuition for how the interpreter works, consider the following
example:\footnote{Code for all examples in the shallow interpreter is available
here
\url{https://github.com/iambrj/metaKanren/blob/master/shallow-examples.rkt}}

\begin{Verbatim}
(run* (x) (eval-programo `(run 1 (z) (== 'cat z))
                          x))
\end{Verbatim}
$\Rightarrow$

\begin{Verbatim}
(cat)
\end{Verbatim}
\smallskip

By definition of the shallow interpreter, the inner query variable \Verb|z| is
unified with the outer query variable \Verb|x|. Therefore, a \Verb|run*| on
\Verb|x| will return all possible values of \Verb|z| that satisfy the given
constraints, explaining the above output.

Now, let us perform program synthesis by providing the expected output and
inserting a logic variable from the outer query in the goal expression of the
inner query:

\begin{Verbatim}
(run* (x) (eval-programo `(run 1 (z) (== ,x z))
                          cat))
\end{Verbatim}
$\Rightarrow$

\begin{Verbatim}
('cat z)
\end{Verbatim}
\smallskip

Perhaps surprisingly, in addition to the expected \Verb|'cat| we get back the
unexpected \Verb|z|. Because interpreted logic variables are represented as
interpreting logic variables, the unification \Verb|(== z z)| in the inner query
succeeds for \textit{all} values of the outer query variable, since by
definition every value unifies with itself! Thus, in particular, the inner
\Verb|run| succeeds when the outer query variable unifies with \Verb|'cat|
because \Verb|(== 'cat 'cat)| succeeds.

Now consider this example, where we try to synthesize the branches of a
disjunction:

\begin{Verbatim}
(run 4 (e1 e2) (eval-programo `(run 1 (z) (disj ,e1
                                                ,e2))
                              'cat))
\end{Verbatim}
$\Rightarrow$

\begin{Verbatim}
'(((== '_.0 '_.0) _.1)
  (_.0 (== '_.1 '_.1))
  ((== 'cat z) _.0)
  (_.0 (== 'cat z)))
\end{Verbatim}
\smallskip

Note that we use a \Verb|run 4| instead of a \Verb|run*| for the outer query
because there are infinitely many correct ways to synthesize \Verb|e1| and
\Verb|e2|. The first two answers are explained by the behaviour explained above,
except we have fresh logic variables (such as \Verb|'_.0| and \Verb|'_.1|)
unifying with themselves instead of ground terms (such as \Verb|z|). The next
two however, are more interesting. They tell us that if one of the branches
unifies the inner query variable \Verb|z| with \Verb|'cat|, the other branch can
be \textit{anything}. This, while correct, is not always desirable and imposes a
strong limitation on the expressivity of the synthesized programs.  Concretely,
we can never ``close-off'' a disjunction (i.e. the interpreter always
synthesizes branches with logic variables, instead of branches with concrete
expressions) when the inner query is restricted to a \Verb|run 1| semantics,
because with a \Verb|run 1| semantics we can only ever say ``so-and-so should be
\textit{one of the} answers'' but we can never say ``so-and-so should be the
\textit{only} answer(s)''!  If we had embedded \Verb|run*| semantics, then
observe that we can express the latter.

To have \Verb|run*| semantics at the interpreted level, \Verb|eval-programo|
express when goals fail. To see why, consider how \Verb|eval-programo| would
interpret the following example:
\begin{Verbatim}
(run 1 (e1 e2)
  (eval-programo `(run* (z) (disj ,e1
                                  ,e2))
                 'cat))
\end{Verbatim}

To interpret the \Verb|run*|, \Verb|eval-programo| must express that one of the
outer logic variables \Verb|e1| and \Verb|e2| should succeed by unifying the
inner logic variable \Verb|z| with the symbol \Verb|cat| and \textit{the other
one should fail} (so that no other answers are produced). As interpreted goals
are represented as miniKanren goals, this implies \Verb|eval-programo| is to
express when a miniKanren goals fails. But this goes against the relational
nature of miniKanren!

If \Verb|eval-programo| were a deep embedding (i.e. used first-order semantic
representations) the above limitation can be overcome since success/failure of
goals can be expressed as conditions on the semantic representations, preserving
relationality. This motivates the need for a deep embedding of miniKanren.

